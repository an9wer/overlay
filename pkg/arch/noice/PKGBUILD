_commit=bfe589a
pkgname=noice
pkgver=0.8.r40.gbfe589a
pkgrel=2
pkgdesc='A small curses-based file browser'
url='http://git.2f30.org/noice/'
arch=('x86_64')
licenses=('BSD')
makedepends=('git' 'make' 'gcc' 'fakeroot')
source=(noiceconf.h
        nopenconf.h
        select.sh
        list.sh
        move.sh
        copy.sh
        "$pkgname::git://git.2f30.org/noice#commit=$_commit")
md5sums=('8f88979fe8d3365062048f966caa5d9e'
         'c000256bb43c7ccfedec6b469d5452c8'
         'fb3c4a46fa45961e81d13f75f99fa2d9'
         'd7217fc1990d42a8ced33a2c29e1dd7a'
         '2f9a71c465e3b08da1a27be6bb118e34'
         'b0d90b6645de6acb7d4c573d65a35f3e'
         'SKIP')

prepare() {
  cd "$pkgname"
  cp "$srcdir/noiceconf.h" noiceconf.h
  cp "$srcdir/nopenconf.h" nopenconf.h
}

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$pkgname"
  make
}

package() {
  install -m755 -D list.sh "$pkgdir/usr/lib/noice/list.sh"
  install -m755 -D move.sh "$pkgdir/usr/lib/noice/move.sh"
  install -m755 -D copy.sh "$pkgdir/usr/lib/noice/copy.sh"
  install -m755 -D select.sh "$pkgdir/usr/lib/noice/select.sh"

  cd "$pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README  "$pkgdir/usr/share/doc/$pkgname/README"
}
