_commit=db6093f
pkgname=dmenu
pkgver=4.9.r5.gdb6093f
pkgrel=2
pkgdesc="An efficient dynamic menu for X."
url="https://tools.suckless.org/dmenu/"
arch=('x86_64')
license=('MIT')
makedepends=('git' 'make' 'gcc' 'fakeroot')
depends=('libx11' 'libxinerama' 'libxft')
source=(config.h
        dmenu_pass
        dmenu_pass_run
        "$pkgname::git://git.suckless.org/dmenu")
md5sums=('797055fb501285c71c97b85254b8d2c5'
         '455aee5614ffff135a895ef0d8c8f1ed'
         '6876ba61f96acb8f125f645915e3427e'
         'SKIP')

prepare() {
  cd "$pkgname"
  cp "$srcdir/config.h" config.h
}

pkgver() {
  cd "$pkgname"
  git checkout "$_commit"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$pkgname"
  make
}

package() {
  install -m755 -D dmenu_pass "$pkgdir/usr/bin/dmenu_pass"
  install -m755 -D dmenu_pass_run "$pkgdir/usr/bin/dmenu_pass_run"

  cd "$pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README  "$pkgdir/usr/share/doc/$pkgname/README"
}
