_commit=cb3f58a
pkgname=dwm
pkgver=6.2.r0.gcb3f58a
pkgrel=3
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('x86_64')
license=('MIT')
makedepends=('git' 'patch' 'make' 'gcc' 'fakeroot')
depends=('libx11' 'libxft' 'libxinerama')
source=(config.h
        dwm.patch
        "$pkgname::git://git.suckless.org/dwm#commit=$_commit")
md5sums=('0d0dc5323f3e00e6c62f9f62ead3535c'
         'ee94a4f98596ab6730e1e97a2703cdd2'
         'SKIP')

prepare() {
  cd "$pkgname"
  cp "$srcdir/config.h" config.h
  patch --forward --strip=1 --input="${srcdir}/dwm.patch"
}

pkgver() {
  cd "$pkgname"
  git stash save --quiet
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  git stash pop --quiet
}

build() {
  cd "$pkgname"
  make
}

package() {
  cd "$pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README  "$pkgdir/usr/share/doc/$pkgname/README"
}
