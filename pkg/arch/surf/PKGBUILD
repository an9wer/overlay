_commit=d068a38
pkgname=surf
pkgver=2.0.r73.gd068a38
pkgrel=1
pkgdesc="A simple Web browser based on WebKit/GTK+"
url="https://surf.suckless.org/"
arch=('x86_64')
license=('MIT')
makedepends=('git' 'make' 'gcc' 'pkg-config' 'fakeroot')
depends=('webkit2gtk' 'gcr')
source=(config.h
        "$pkgname::git://git.suckless.org/surf#commit=$_commit")
md5sums=('970ff475955b57ee8d2d60243fb46b05'
         'SKIP')

prepare() {
  cd "$pkgname"
  cp "$srcdir/config.h" config.h
}

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$pkgname"
  make
}

package() {
  cd "$pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README  "$pkgdir/usr/share/doc/$pkgname/README"
}
